buildscript {
    ext {
        tsbiFrameworkVersion = "3.2"
        tsbiBomVersion = "0.0.19"
        springBootVersion = "3.3.2"

        securityBomVersion = "6.12.2"

        checkstyleVersion = "10.3.4"
        pmdVersion = "6.55.0"
        jacocoVersion = "0.8.11"
        spotbugsVersion = "4.8.1"
        spotbugsPluginVersion = "4.7.5"
        sonarqubePluginVersion = "5.1.0.4882"

        // workaround for reckon issue with Java 8
        grgitVersion = "5.2.2"

        testngVersion = "7.10.2"
        jakartaVersion = "6.1.0"
        jakartaValidationVersion = "3.1.0"

        lombokVersion = "1.18.34"

    }
    repositories {
        gradlePluginPortal()
    }

    configurations.all {
        resolutionStrategy {
            cacheDynamicVersionsFor 0, 'seconds'
        }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:$spotbugsPluginVersion"

        // workaround for reckon issue with Java 8
        classpath "org.ajoberstar.grgit:grgit-gradle:$grgitVersion"
    }
}

plugins {
    id "org.sonarqube" version "$sonarqubePluginVersion"
}

if (!hasProperty('buildTimestamp')) {
    ext.buildTimestamp = new Date().format('yyyyMMddHHmmss', TimeZone.getTimeZone('UTC'))
}

configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor 0, 'seconds'
    }
    exclude group: "log4j", module: "log4j"
    exclude group: "org.apache.logging.log4j"
    exclude group: "org.slf4j", module: "slf4j-log4j12"
}

subprojects {
    group = "org.talend"

    apply from: rootProject.file('config/java.gradle')
    apply from: rootProject.file('config/publish.gradle')

    repositories {
        mavenCentral()

        maven {
            url 'https://artifacts-zl.talend.com/nexus/content/repositories/tpsvc'
            credentials {
                username nexusUsername
                password nexusPassword
            }
        }

        maven {
            url 'https://artifacts-zl.talend.com/nexus/content/repositories/releases/'
            credentials {
                username nexusTalendReleasesUsername
                password nexusTalendReleasesPassword
            }
        }

        maven {
            url 'https://artifacts-zl.talend.com/nexus/content/repositories/snapshots/'
            credentials {
                username nexusTalendSnapshotsUsername
                password nexusTalendSnapshotsPassword
            }
        }

        maven {
            url 'https://artifacts-zl.talend.com/nexus/content/groups/public'
        }
    }
}

// This task is used by Jenkinsfile
tasks.register('versionInformation') {
    doLast {
        if (project.hasProperty("version")) {
            new File("$projectDir/version.txt").text = "$version"
        } else {
            throw new GradleException('No version properties')
        }
    }
}
